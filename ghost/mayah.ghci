;
; [0-127] is used fo
; [128] is used for random
; Register A, B, C, D, E, F won't be preserved after the function call.
;

; OK
INITIALIZE_IF_NECESSARY:
        JEQ INITIALIZE, [129], 1
        INT 3
        MOV [128], A
        MOV [129], 1

; OK
INITIALIZE:
        INT 1
        MOV [133], A
        MOV [134], B
        INT 3
        MOV [130], A
        INT 5
        MOV [131], A
        MOV [132], B

MAIN:
        ; Check the enemy is in the same row.
        ; In this case, we try to follow the lambdaman.
        JEQ FUNCTION_MAYBE_CHASE_Y, [131], [133]
        JEQ FUNCTION_MAYBE_CHASE_X, [132], [134]

        MOV G, MAIN_AFTER_RANDOM
        MOV PC, FUNCTION_MAKE_RANDOM
MAIN_AFTER_RANDOM:
        DIV A, 16
        AND A, 3
        JEQ FUNCTION_RANDOM_MOVE, A, 0
        MOV PC, FUNCTION_CHASE


; chase
FUNCTION_CHASE:
        MOV G, FC_AFTER_RANDOM
        MOV PC, FUNCTION_MAKE_RANDOM
FC_AFTER_RANDOM:
        AND A, 1
        JEQ FUNCTION_CHASE_X, A, 0
        MOV PC, FUNCTION_CHASE_Y


; OK
FUNCTION_MAYBE_CHASE_X:
        MOV G, FMCX_RANDOM
        MOV PC, FUNCTION_MAKE_RANDOM
FMCX_RANDOM:
        DIV A, 8
        AND A, 7
        INT 8
        JEQ FUNCTION_RANDOM_MOVE, A, 0
        MOV PC, FUNCTION_CHASE_X

; OK
FUNCTION_MAYBE_CHASE_Y:
        MOV G, FMCY_RANDOM
        MOV PC, FUNCTION_MAKE_RANDOM
FMCY_RANDOM:
        DIV A, 8
        AND A, 7
        JEQ FUNCTION_RANDOM_MOVE, A, 0
        MOV PC, FUNCTION_CHASE_Y

; OK
FUNCTION_CHASE_X:
        JLT FUNCTION_GO_RIGHT, [131], [133]
        MOV PC, FUNCTION_GO_LEFT

; OK
FUNCTION_CHASE_Y:
        JLT FUNCTION_GO_DOWN, [132], [134]
        MOV PC, FUNCTION_GO_UP

; OK
FUNCTION_MAKE_RANDOM:
        ; Generates a random number in Register A.
        ; RETURN in G

        ; take 3bit xor 8 bit.
        MOV A, [128]
        MOV B, A
        DIV A, 128
        DIV B, 4
        AND B, 1
        XOR B, A
        ;
        MOV A, [128]
        MUL A, 2
        OR  A, B
        ADD A, 130
        MOV [128], A
        MOV PC, G

; OK
FUNCTION_RANDOM_MOVE:
        MOV G, RANDOM_MOVE_AFTER_RANDOM
        MOV PC, FUNCTION_MAKE_RANDOM
RANDOM_MOVE_AFTER_RANDOM:
        DIV A, 16
        AND A, 3
        INT 0
        HLT

; OK
FUNCTION_GO_UP:
        MOV A, 0
        INT 0
        HLT

; OK
FUNCTION_GO_RIGHT:
        MOV A, 1
        INT 0
        HLT

; OK
FUNCTION_GO_DOWN:
        MOV A, 2
        INT 0
        HLT

; OK
FUNCTION_GO_LEFT:
        MOV A, 3
        INT 0
        HLT
